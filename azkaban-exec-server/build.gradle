apply plugin: 'distribution'

dependencies {
    compile(project(':az-core'))
    compile(project(':azkaban-common'))

    compile deps.kafkaLog4jAppender
    compile(deps.gobblinKafka) {
        exclude group: 'org.apache.hadoop'
    }

    runtime(project(':azkaban-hadoop-security-plugin'))

    testCompile(project(path: ':azkaban-common', configuration: 'testCompile'))
    testCompile(project(':azkaban-common').sourceSets.test.output)

    testCompile deps.h2
    testCompile deps.hadoopAnnotations
    testCompile deps.hadoopAuth
    testCompile deps.hadoopCommon
    testCompile deps.hadoopHdfs
}

configurations.compile {
    exclude group: 'com.linkedin.gobblin', module: 'gobblin-api'
    exclude group: 'com.linkedin.gobblin', module: 'gobblin-metrics-graphite'
    exclude group: 'com.linkedin.gobblin', module: 'gobblin-metrics-hadoop'
    exclude group: 'com.linkedin.gobblin', module: 'gobblin-metrics-influxdb'
    exclude group: 'com.linkedin.gobblin', module: 'gobblin-runtime'

    exclude group: 'org.projectlombok', module: 'lombok'
}

distributions {
    main {
        contents {
            from('src/main/bash') {
                into 'bin'
                fileMode = 0755
            }
            from('../azkaban-common/src/main/bash') {
                into 'bin'
                fileMode = 0755
            }
            from(configurations.runtime) {
                into 'lib'
            }
            from(jar) {
                into 'lib'
            }
        }
    }
}
