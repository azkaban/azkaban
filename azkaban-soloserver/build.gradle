apply plugin: 'distribution'

dependencies {
  compile(project(':azkaban-common'))
  compile(project(':azkaban-webserver'))
  compile(project(':azkaban-execserver'))
}

installDist.dependsOn ':azkaban-webserver:installDist'
distTar.dependsOn ':azkaban-webserver:installDist'
distZip.dependsOn ':azkaban-webserver:installDist'

distributions {
  main {
    contents {
      from('src/main/bash') {
        into 'bin'
        fileMode = 0755
      }
      from ('src/main/resources/conf') {
        into 'conf'
      }
      from(configurations.runtime) {
        into 'lib'
      }
      from(jar) {
        into 'lib'
      }
      from(project(':azkaban-sql').files('src/sql')) {
        into('sql')
      }
      // Get the files from sub-dir of distribution of another sub-project
      from(new File(tasks.getByPath(':azkaban-webserver:installDist').destinationDir, 'web')) {
        into 'web'
      }
    }
  }
}
