/*
 * Copyright 2017 LinkedIn Corp.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations under
 * the License.
 *
 */
apply plugin: 'distribution'

task testJar(type: Jar, dependsOn: testClasses) {
    from sourceSets.test.output
    classifier = 'test'
}

configurations {
    testOutput.extendsFrom(testCompile)
}

artifacts {
    testOutput testJar
}

dependencies {
    compile project(':az-core')

    // todo kunkun-tang: consolidate dependencies in azkaban-common and azkaban-db
    compile deps.dbutils
    compile deps.dbcp2
    compile deps.io

    testCompile project(path: ':az-core', configuration: 'testOutput')
    testRuntime deps.h2
}

/**
 * A filter that can be used while copying SQL files to convert some data types and syntax that
 * are different between MySQL and PostgreSQL.
 */
def mysqlToPostgresqlFilter = {
    it.replaceAll(/(?i)\bTINYINT\b/, 'SMALLINT')
        .replaceAll(/(?i)\bBIGINT\(\d+\)/, 'BIGINT')
        .replaceAll(/(?i)\bSMALLINT\(\d+\)/, 'SMALLINT')
        .replaceAll(/(?i)\b(LONG)?BLOB\b/, 'BYTEA')
        .replaceAll(/(?i)\bBINARY\(\d+\)/, 'BYTEA')
        .replaceAll(/(?i)\bDATETIME\b/, 'TIMESTAMP')
        .replaceAll(/(?i)\bAUTO_INCREMENT\b/, 'GENERATED ALWAYS AS IDENTITY')
}

/**
 * concat task
 * Concatenate all the sql queries / files (except upgrade.*.sql) and collect it into a single
 * SQL file.
 */
task concat() {
    doLast {
        ext.destFileMysql = "${buildDir}/sql/create-all-mysql-" + version + '.sql';
        ant.concat(destfile: destFileMysql, fixlastline: 'yes') {
            logger.info('Concating create scripts to ' + destFileMysql)
            fileset(dir: 'src/main/sql') {
                include(name: 'create.*.sql')
                exclude(name: '*-postresql.sql')
            }
        }
        ext.intFilePostgresql = 'build/tmp/sql/create-all-postgresql-intermediate-' + version + '' +
                '.sql';
        ant.concat(destfile: intFilePostgresql, fixlastline: 'yes') {
            logger.info('Concating create scripts to ' + intFilePostgresql)
            fileset(dir: 'src/main/sql') {
                include(name: 'create.*.sql')
                exclude(name: '*-mysql.sql')
            }
        }
        copy {
            from(intFilePostgresql) {
                rename 'postgresql-intermediate', 'postgresql'
                filter mysqlToPostgresqlFilter
            }
            into 'build/sql'
        }
    }
}

installDist.dependsOn concat
distTar.dependsOn concat
distZip.dependsOn concat

distributions {
    main {
        contents {
            from('src/main/sql')
            from('src/main/sql') {
                rename '\\.sql$', '.postgresql.sql'
                filter mysqlToPostgresqlFilter
            }
            from("$buildDir/sql")
        }
    }
}
